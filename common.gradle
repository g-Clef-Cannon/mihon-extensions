apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 34

    namespace "eu.kanade.tachiyomi.extension"
    
    sourceSets {
        main {
            manifest.srcFile layout.buildDirectory.file('tempAndroidManifest.xml')
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    
    defaultConfig {
        minSdk 21
        targetSdk 34
        applicationIdSuffix project.parent.name + "." + project.name
        versionCode extVersionCode
        versionName "1.4.$versionCode"
        
        manifestPlaceholders = [
            appName : "Tachiyomi: $extName",
            extClass: extClass,
            nsfw    : project.ext.find("isNsfw") ? 1 : 0,
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

repositories {
    mavenCentral()
    google()
    maven { url = "https://jitpack.io" }
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.9.10")
    compileOnly("com.github.tachiyomiorg:extensions-lib:1.4.4")
    compileOnly("androidx.preference:preference-ktx:1.2.1")
    
    implementation("com.google.code.gson:gson:2.10.1")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("org.jsoup:jsoup:1.17.2")
    implementation("com.squareup.okio:okio:3.7.0")
    
    compileOnly("io.reactivex:rxandroid:1.2.1")
    compileOnly("io.reactivex:rxjava:1.3.8")
}

tasks.register("writeManifestFile") {
    doLast {
        File tempFile = android.sourceSets.getByName('main').manifest.srcFile
        if (!tempFile.exists()) {
            tempFile.parentFile.mkdirs()
            tempFile.write("""<?xml version="1.0" encoding="utf-8"?>
<manifest xmlns:android="http://schemas.android.com/apk/res/android">
    <uses-feature
        android:name="tachiyomi.extension"
        android:required="false" />
    <application
        android:label="\${appName}"
        android:icon="@mipmap/ic_launcher">
        <meta-data
            android:name="tachiyomi.extension.class"
            android:value="\${extClass}" />
        <meta-data
            android:name="tachiyomi.extension.nsfw"
            android:value="\${nsfw}" />
    </application>
</manifest>
""")
        }
    }
}

preBuild.dependsOn(writeManifestFile)
